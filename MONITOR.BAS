    5 REM BASIC MONITOR BY ROBERT LOWE (PNGWEN)
    6 REM   ORIGINAL: LONG AGO ON A C64
    7 REM   AGON LIGHT VERSION: 10 APRIL 2023
   10 PRINT : PRINT "MONITOR OPERATION"
   20 PRINT "  1.) READ MEMORY"
   30 PRINT "  2.) WRITE MEMORY"
   40 PRINT "  3.) RUN PROGRAM"
   45 PRINT "  4.) QUIT"
   50 INPUT "YOUR CHOICE";C
   60 PRINT 
   70 IF C>=1 THEN  IF C<=3 THEN  GOTO 90
   75 IF C=4 THEN  END
   80 PRINT "INVALID CHOICE. PLEASE TRY AGAIN.": GOTO 10
   90 INPUT "ADDRESS";H$
  100 GOSUB 2000
  110 A=H
  120 IF C=3 THEN  CALL A: GOTO 10
  130 IF C=2 THEN  GOTO 300
  140 INPUT "HOW MANY BYTES (IN HEX)";H$: GOSUB 2000
  150 N=H
  160 I=A:E=A+N-1: GOSUB 250
  170 H=?I
  180 GOSUB 2500: PRINT " ";
  190 I=I+1
  200 IF I>E THEN  PRINT : GOTO 10
  210 IF I/8 - INT(I/8) = 0 THEN PRINT : GOSUB 250
  220 GOTO 170
  250 H=I
  260 GOSUB 2500
  270 PRINT ": ";
  280 RETURN 
  300 PRINT "ENTER BYTES. '.' TO STOP."
  310 INPUT H$
  320 IF H$="." THEN  GOTO 10
  330 GOSUB 2000
  340 ?A=H
  350 A=A+1
  360 GOTO 310
 2000 REM CONVERT HEX STRING H$ TO INTEGER H
 2010 H=0
 2020 FOR I=1 TO LEN(H$)
 2030   D=ASC(MID$(H$,I,1))-48
 2040   IF D>9 THEN D=D-7
 2050   H=H*16+D
 2060 NEXT I
 2070 RETURN 
 2500 REM PRINT THE INTEGER H IN HEX
 2510 Q=16
 2520 IF H>255 THEN Q=4096
 2530 D=INT(H/Q)
 2540 H=H-D*Q
 2550 Q=Q/16
 2560 IF D>9 THEN D=D+7
 2570 PRINT CHR$(D+48);
 2580 IF Q>=1 THEN  GOTO 2530
 2590 RETURN 
49153 AND 7
